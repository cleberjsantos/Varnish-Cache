varnishtest "Test std & debug vmod"

server s1 {
	rxreq 
	txresp -hdr "foo: bAr" -hdr "bar: fOo" -bodylen 4
} -start

varnish v1 -vcl+backend {
	import std from "${topbuild}/lib/libvmod_std/.libs/libvmod_std.so" ;
	import debug from "${topbuild}/lib/libvmod_debug/.libs/libvmod_debug.so" ;

	sub vcl_deliver {
		set resp.http.foo = std.toupper(resp.http.foo);
		set resp.http.bar = std.tolower(resp.http.bar);
		set resp.http.who = debug.author(phk);
		debug.test_priv_call();
		debug.test_priv_vcl();
		std.log("VCL initiated log");
		std.syslog(8 + 7, "Somebody runs varnishtest");
	}
} -start

client c1 {
	txreq -url "/bar"
	rxresp
	expect resp.status == 200
	expect resp.bodylen == "4"
	expect resp.http.foo == "BAR"
	expect resp.http.bar == "foo"
} -run

varnish v1 -badvcl {
	import debug from "${topbuild}/lib/libvmod_debug/.libs/libvmod_debug.so.1" ;
	sub vcl_deliver {
		set resp.http.who = debug.author(jfk);
	}
}

varnish v1 -badvcl {
	import std from "${topbuild}/lib/libvmod_std/.libs/libvmod_std.so.1" ;
	sub vcl_deliver {
		set resp.http.who = std.random("foo", "bar");
	}
}
